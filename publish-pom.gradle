android {
    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
            withSourcesJar()
        }
    }
}

version = libs.versions.aap.core.get()

afterEvaluate {
    // based on https://zenn.dev/orangain/articles/publish-to-maven-central-using-gradle#fn-c896-2
    // Note that this will build valid archive only on clean environment like CI builds.
    //
    // It is technically unnecessary anymore...
    tasks.register("makeArchive", Zip) {
        dependsOn("publishReleasePublicationToMavenRepository")
        from layout.buildDirectory.dir("stub-install")
        into "."
    }

    tasks.register('sourcesJar', Jar) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
        archiveClassifier.set('sources')
    }

    publishing {
        repositories {
            maven {
                // Install once into fake directory (not to mavenLocal, because it can skip signing)
                url = uri(layout.buildDirectory.dir("stub-install"))
            }
        }

        publications {
            release(MavenPublication) {
                version = android.defaultConfig.versionName
                from components.release
            }

            configureEach {
                // https://github.com/gradle/gradle/issues/26091#issuecomment-1681343496
                var dokkaJar = project.tasks.register("${name}DokkaJar", Jar) {
                    group = JavaBasePlugin.DOCUMENTATION_GROUP
                    description = "Assembles Kotlin docs with Dokka into a Javadoc jar"
                    archiveClassifier.set("javadoc")
                    from(tasks.named("dokkaHtml"))

                    // Each archive name should be distinct, to avoid implicit dependency issues.
                    // We use the same format as the sources Jar tasks.
                    // https://youtrack.jetbrains.com/issue/KT-46466
                    archiveBaseName.set("${archiveBaseName.get()}-${name}")
                }
                artifact(dokkaJar)

                pom {
                    name.set(project.name)
                    description.set(android.ext.description)
                    url.set("https://github.com/atsushieno/aap-core")
                    scm {
                        url.set("https://github.com/atsushieno/aap-core")
                    }
                    licenses {
                        license {
                            name.set("the MIT License")
                            url.set("https://github.com/atsushieno/aap-core/blob/main/LICENSE")
                        }
                    }
                    developers {
                        developer {
                            id.set("atsushieno")
                            name.set("Atsushi Eno")
                            email.set("atsushieno@gmail.com")
                        }
                    }
                }
            }
        }
    }

    // based on https://zenn.dev/orangain/articles/publish-to-maven-central-using-gradle#fn-c896-2
    tasks.named("sonatypeCentralUpload") {
        dependsOn("makeArchive")

        username = System.getenv("OSSRH_USERNAME")
        password = System.getenv("OSSRH_PASSWORD")

        archives = files(layout.buildDirectory.dir("stub-install").get().asFileTree.files)
        pom = file(tasks.named("generatePomFileForReleasePublication").get().outputs.files.singleFile)

        signingKey = System.getenv("SIGNING_KEY_ID")
        signingKeyPassphrase = System.getenv("SIGNING_PASSWORD")
    }

    // keep it as is. It is replaced by CI release builds
    signing {}
}
